ext {
    javaeeVersion = '7.0'
    domaVersion = '2.6.1'
    h2Version = '1.4.190'
    flywayVersion = '3.2.1'
    thymeleafVersion = '2.1.4.RELEASE'
    slf4jVersion = '1.6.6'
    superagentVersion = '1.4.0'
    auiVersion = '5.9.5'
    jqueryVersion = '1.8.3'
    junitVersion = '4.12'
    jerseyVersion = '2.22.1'
    assertjVersion = '3.3.0'
}

apply plugin: 'java'
apply plugin: 'war'
apply from: 'eclipse.gradle'

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

compileJava.dependsOn processResources
processResources.destinationDir = compileJava.destinationDir

// Gradleと@RunWith(Enclosed.class)なテストクラスの相性がよろしくない。
// statisにネストしたテストクラスを除外して@RunWith(Enclosed.class)の実行だけに絞る。
// http://d.hatena.ne.jp/shuji_w6e/20120808/1344386399
test.exclude '**/*$*'

repositories {
    jcenter()
}

dependencies {
    providedCompile "javax:javaee-api:${javaeeVersion}"
    compile "org.seasar.doma:doma:${domaVersion}"
    compile "com.h2database:h2:${h2Version}"
    compile "org.flywaydb:flyway-core:${flywayVersion}"
    compile "org.thymeleaf:thymeleaf:${thymeleafVersion}"
    runtime "org.slf4j:slf4j-jdk14:${slf4jVersion}"
    compile "org.webjars:superagent:${superagentVersion}"
    compile "org.webjars:aui:${auiVersion}"
    compile "org.webjars:jquery:${jqueryVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.assertj:assertj-core:${assertjVersion}"
    testCompile "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:${jerseyVersion}"
    archives 'fish.payara.extras:payara-micro:4.1.1.154'
}

task run(type:Exec) {
    def payaraJar = configurations.archives.find { it.name ==~ /payara-micro.*/ }
    def warFile = war.archivePath
    commandLine 'java', '-Ddb.url=jdbc:h2:file:./build/data', '-jar', "${payaraJar}", '--noCluster', '--deploy', "${warFile}"
}

run.dependsOn war

task h2(type: Exec) {
    def h2Jar = configurations.compile.find { it.name ==~ /h2.*/ }
    commandLine 'java', '-jar', "${h2Jar}"
}
